---
title: 4.3. On-chain indexes
sidebar_position: 3
---

# Non-Fungible Token on-chain indexes (TIP-4.3)
## Abstract
Using the Index contract code, you can find all your NFT with one query to [DApp](https://main.ton.dev/graphql)

## Motivation
A standard interface allows search any NFT using base [DApp](https://main.ton.dev/graphql) functionality

## Specification
* `Collection` - contract that minted token
* `NFT` - contract that store token information
* `IndexBasis` -  contract, that help to find all collections by the `code hash` of which
* `Index` - contract, that help to find:
  * All user tokens in current collection using `owner address` and `collection address`
  * All user tokens in all collections using `owner address`

`Code` of `IndexBasis` and `Index` contracts and `code hash` of contract is fixed and **CANNOT BE CHANGED**

## IIndexBasis
```solidity
pragma ton-solidity >= 0.58.0;

interface IIndexBasis {
    function getInfo() external view responsible returns (address collection);
    function destruct(address gasReceiver) external;
}
```

## IndexBasis
```solidity
pragma ton-solidity >= 0.58.0;

import './IIndexBasis.sol';

/**
 * Errors
 *   101 - Method for collection only
 **/
contract IndexBasis is IIndexBasis {
    address static _collection;

    modifier onlyCollection() {
        require(msg.sender == _collection, 101, "Method for collection only");
        tvm.accept();
        _;
    }

    constructor() public onlyCollection {}

    function getInfo() override public view responsible returns (address collection) {
        return {value: 0, flag: 64} _collection;
    }

    function destruct(address gasReceiver) override public onlyCollection {
        selfdestruct(gasReceiver);
    }
}
```

`IndexBasis` `code hash` is `8b36621923212d315cc719a47f5aac4f97b2e044863fdb8a7d600c0765064257`

### IIndexBasis.getInfo()
```solidity
function getInfo() external view responsible returns (address collection);
```

* `collection` (`address`) - address of collection token contract

### IIndexBasis.destruct()
```solidity
    function destruct(address gasReceiver) external;
```

* `gasReceiver` (`address`) - address of contract that receive all remaining contract balance after `selfdestruct()` call

## IIndex
```solidity
pragma ton-solidity >= 0.58.0;

interface IIndex {
    function getInfo() external view responsible returns (
        address collection,
        address owner,
        address nft
    );
    function destruct(address gasReceiver) external;
}
```

## Index
```solidity
pragma ton-solidity >= 0.58.0;

import './IIndex.sol';

/**
 * Errors
 *   101 - Method for NFT only
 *   102 - Salt doesn't contain any value
 **/
contract Index is IIndex {
    address static _nft;
    
    address _collection;
    address _owner;

    constructor(address collection) public {
        optional(TvmCell) salt = tvm.codeSalt(tvm.code());
        require(salt.hasValue(), 102, "Salt doesn't contain any value");
        (, address collection_, address owner) = salt
            .get()
            .toSlice()
            .decode(string, address, address);
        require(msg.sender == _nft);
        tvm.accept();
        _collection = collection_;
        _owner = owner;
        if (collection_.value == 0) {
            _collection = collection;
        }
    }

    function getInfo() override public view responsible returns (
        address collection,
        address owner,
        address nft
    ) {
        return {value: 0, flag: 64} (
            _collection,
            _owner,
            _nft
        );
    }

    function destruct(address gasReceiver) override public {
        require(msg.sender == _nft, 101, "Method for NFT only");
        selfdestruct(gasReceiver);
    }
}
```

`Index` `code hash` is `61e5f39a693dc133ea8faf3e80fac069250161b0bced3790c20ae234ce6fd866`

### Index.getInfo()
```solidity
function getInfo() external view responsible returns (
    address collection,
    address owner,
    address nft
);
```

* `collection` (`address`) - address of collection token contract
* `owner` (`address`) - address of token owner contract
* `nft` (`address`) - address of token contract

### IIndexBasis.destruct()
```solidity
    function destruct(address gasReceiver) external;
```

* `gasReceiver` (`address`) - address of contract that receive all remaining contract balance after `selfdestruct()` call


## Salt
Use [tvm.setCodeSalt()](https://github.com/tonlabs/TON-Solidity-Compiler/blob/master/API.md#tvmcodesalt) for `IndexBasis` or `Index` deployment

Example:
```solidity
function deployIndexBasis(TvmCell codeIndex, address collection) private pure {
    string stamp = "nft";
    TvmBuilder salt;
    salt.store(stamp);
    TvmCell code = tvm.setCodeSalt(codeIndex, salt.toCell());
    TvmCell stateInit = tvm.buildStateInit({
        contr: IndexBasis,
        varInit: {_collection: collection},
        code: code
    });
    new IndexBasis{stateInit: stateInit, value: 1 ton}();
}
```

To deploy the index, you need to add a salt to the code with the `stamp` (`string`).
`stamp` is needed to get a **unique** `code hash`.
The unique `code hash` is needed to avoid collisions with other index contracts that are used to index other types of contracts

### For NFT contracts use:
```solidity
string stamp = "nft";
```

For other types of contracts, you must use another `stamp`


## Visualization
### Legend
<img src="images/legend.svg" width="100%">

### `IndexBasis` deployment for `Collection`
<img src="images/index1.svg" width="100%">

### `Index` contracts deployment for `NFT`
<img src="images/index2.svg" width="100%">

### Redeploy `Index` contracts after `changeOwner`
<img src="images/index3.svg" width="100%">